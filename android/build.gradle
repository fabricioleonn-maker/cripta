// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "34.0.0"
        minSdkVersion = 21
        compileSdkVersion = 34
        targetSdkVersion = 34

        // We use NDK 23 which has both M1 support and is the side-by-side NDK version from AGP.
        ndkVersion = "23.1.7779620"
        kotlinVersion = "1.9.20"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.2.2")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    // Force all Kotlin tasks to use JVM 17
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "17"
            freeCompilerArgs += ["-Xjvm-default=all"]
        }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }

                // AGP 8.1+ requires a namespace. If a library is missing it, we inject it.
                if (project.plugins.hasPlugin('com.android.library')) {
                    if (namespace == null) {
                        def manifestFile = project.file("src/main/AndroidManifest.xml")
                        if (manifestFile.exists()) {
                            def manifestContent = manifestFile.getText('UTF-8')
                            def matcher = manifestContent =~ /package="([^"]+)"/
                            if (matcher.find()) {
                                namespace = matcher.group(1)
                            } else {
                                namespace = "com.voke.cripta.${project.name.replaceAll(/[^a-zA-Z0-9]/, '.')}"
                            }
                        } else {
                            namespace = "com.voke.cripta.${project.name.replaceAll(/[^a-zA-Z0-9]/, '.')}"
                        }
                    }
                }
            }
        }
    }
}
